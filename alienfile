use alienfile;

use Env qw(@PATH);

plugin 'Probe::CommandLine' => (
  command => 'swig',
  args    => [ '-version' ],
  match   => qr/SWIG Version (4\.[0-9\.]+)/,
  version => qr/SWIG Version (4\.[0-9\.]+)/,
);

plugin 'Build::SearchDep' => (
  aliens => [qw( Alien::PCRE2 )],
);

share {
  requires 'Alien::PCRE2';
  start_url 'https://swig.org/download.html';

  plugin Download => (
    filter  => qr/^swig.*tar\.gz$/,
    version => qr/(4\.[0-9\.]+)/,
  );
  meta->around_hook(
    prefer => sub {
      my $orig = shift;
      my $build = shift;
      my $data = $orig->($build, @_);

      for my $item (@{ $data->{list} }) {
        # turn into https://
        $item->{url} =~ s,^http://(?=\Qprdownloads.sourceforge.net/\E),https://,;
      }

      $data;
  });

  plugin Extract => 'tar.gz';

  meta->around_hook( prefer => sub {
    my($orig, $build) = @_;

    # path for pcre2-config
    push @PATH, Alien::PCRE2->bin_dir;

    $orig->($build);

  });

  plugin 'Build::Autoconf';
  build [
    join(' ',
      '%{configure}',
        '--with-pcre',
    ),
    '%{make}',
    '%{make} install',
  ];
}
